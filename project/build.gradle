buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        //classpath 'org.robovm:robovm-gradle-plugin:1.12.0'
        classpath "com.badlogicgames.gdx:gdx-tools:1.9.8"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '1.0'
    ext {
        appName = "hacking-game"
        gdxVersion = '1.9.8'
        //roboVMVersion = '1.12.0'
        box2DLightsVersion = '1.4'
        ashleyVersion = '1.7.0'
        aiVersion = '1.8.0'
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":desktop") {
    apply plugin: "java"

    dependencies {
        implementation project(":core")
        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":android") {
    apply plugin: "android"

    configurations { natives }

    dependencies {
        implementation project(":core")
        implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
    }
}

project(":core") {
    apply plugin: "java"
    apply plugin: "com.google.protobuf"

    dependencies {
        implementation project(":gen")
        implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
        implementation fileTree(dir: '../libs', include: '*.jar')
        implementation 'com.google.code.gson:gson:2.8.0'
        implementation group: 'org.javatuples', name: 'javatuples', version: '1.2'
        implementation 'com.google.protobuf:protobuf-java:3.3.0'
    }
}

project(":gen") {
    apply plugin: "java"

    dependencies {
        implementation 'com.google.protobuf:protobuf-java:3.3.0'
    }
}

tasks.eclipse.doLast {
    delete ".project"
}

// Store the parameters you want to pass the texturePacker here...
project.ext.texturePacker = [ "raw_assets/img/game", "android/assets/img", "Game_Assets",
                              "raw_assets/img/ui", "android/assets/img", "UI_Assets",
                              "raw_assets/img/character", "android/assets/img", "Character_Assets"]

// Import the texture packer
import com.badlogic.gdx.tools.texturepacker.TexturePacker

// Add a new task that packs the textures for you
task texturePacker << {
    if (project.ext.has('texturePacker')) {
        logger.info "Calling TexturePacker: "+texturePacker
        TexturePacker.process(texturePacker[0], texturePacker[1], texturePacker[2])
        TexturePacker.process(texturePacker[3], texturePacker[4], texturePacker[5])
        TexturePacker.process(texturePacker[6], texturePacker[7], texturePacker[8])
    }
}